plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven'
    id 'signing'
}

group = 'net.wukl'
archivesBaseName = 'exceptionverifier'
version = '0.9.0'

sourceCompatibility = '1.11'
targetCompatibility = '1.11'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    api 'com.google.code.findbugs:jsr305:3.0.2'
    api 'org.jetbrains:annotations:17.0.0'

    implementation 'org.slf4j:slf4j-api:1.7.26'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.4.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'

    testImplementation 'ch.qos.logback:logback-classic:1.2.3'
}

test {
    useJUnitPlatform()

    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = '0.8.3'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'ExceptionVerifier'
                packaging 'jar'
                description 'Simple exception compliance verification'
                url 'https://git.wukl.net/wukl/exception-verifier'

                scm {
                    connection 'scm:git:https://git.wukl.net/wukl/exception-verifier.git'
                    developerConnection 'scm:git:ssh://git@git.wukl.net:wukl/exception-verifier.git'
                    url 'https://git.wukl.net/wukl/exception-verifier.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'wukl'
                        name 'Luc Everse'
                        email 'luc@wukl.net'
                    }
                }
            }
        }
    }
}
